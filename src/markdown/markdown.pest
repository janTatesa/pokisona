word = _{ (!WHITE_SPACE ~ ANY)+ }

modifier_inner = _{ (!PEEK ~ (paragraph_item | text_wrapped))+ }
italic         =  { PUSH("*" | "_") ~ modifier_inner ~ POP }
bold           =  { PUSH("**" | "__") ~ modifier_inner ~ POP }
bold_italic    =  { PUSH("***" | "___") ~ modifier_inner ~ POP }
highlight      =  { PUSH("==") ~ modifier_inner ~ POP }
strikethrough  =  { PUSH("~~") ~ modifier_inner ~ POP }

inline_block_inner = { (!(soft_break | NEWLINE | PEEK) ~ ANY)* }
inline_code_block  = { PUSH("`") ~ inline_block_inner ~ POP }
inline_math_block  = { PUSH("$") ~ inline_block_inner ~ POP }

code_lang          = { word }
code_block_content =  { (!(NEWLINE ~ "```") ~ ANY)* }
code_block         = { "```" ~ code_lang? ~ NEWLINE ~ code_block_content ~ NEWLINE ~ "```"}

math_block = { "$$" ~ math_block_inner ~ "$$"}
math_block_inner = { (!("$$") ~ ANY)* }

callout_type  = { (!("]" | WHITE_SPACE) ~ ANY)+ }
callout_title =  { (!NEWLINE ~ ANY)+ }
callout       =  { ">" ~ " " ~ "[!" ~ callout_type ~ "]" ~ callout_title? ~ NEWLINE ~ (">" ~ " "? ~ line)* }
quote         =  { (">" ~ " "? ~ line)+ }

paragraph_item    = _{soft_break | escaped_char | comment | external_embed | external_link | embed | link | inline_code_block | inline_math_block | bold_italic | bold | italic | highlight | strikethrough | math_block | tag | reference }
text_wrapped =  { (!(PEEK | paragraph_item | NEWLINE) ~ ANY)+ }
text         =  { (!(paragraph_item | paragraph_break) ~ ANY)+ }
paragraph = {(!(paragraph_break | body_item) ~ (text | paragraph_item))+ ~ (paragraph_break | body_item | &EOI) }
line = {(!hard_break ~ (text | paragraph_item))* ~ hard_break}

paragraph_break = _{"\n" ~ "\n"+}

tag       = { "#" ~ word }
// TODO: references should be attached to a specific item
reference = { "^" ~ word } 

indentation = { "\t"  | "  " }
list_item   = { PUSH(indentation*) ~ (task_done | task_due | bullet | numbered) ~ " " ~ line ~  list_item_indented* ~ DROP }
list_item_indented = _{&(PEEK ~ indentation+) ~ list_item}
bullet      = { "-" }
task_due    = { "- [ ]" }
task_done   = { "- [x]" }
numbered    = { ASCII_DIGIT+ ~ "." }

external_link_display          =  { (!("]" | NEWLINE) ~ ANY)+ }
external_link_url_angled       = _{ "<" ~ external_link_url_angled_inner ~ ">" }
external_link_url_angled_inner =  { (!(">" | NEWLINE) ~ ANY)* }
external_link_url              = { (!("\\" | ")" | WHITE_SPACE) ~ ANY)* }
external_link                  =  { "[" ~ external_link_display ~ "](" ~ (external_link_url_angled | external_link_url) ~ ")" }
external_embed                 = { "![]" ~ "(" ~ (external_link_url_angled | external_link_url) ~ ")" }

file_link      = { (!(NEWLINE | "|" | "]]" | "#") ~ ANY)* }
reference_link = { "^" ~ (!("]]" | WHITE_SPACE) ~ ANY)+ }
heading_link   = { (!(NEWLINE | "|" | "]]") ~ ANY)+ }
link_display   =  { (!(NEWLINE | "]]") ~ ANY)* }
link_target    = { file_link? ~ ("#" ~ (reference_link | heading_link))? }
link           =  { "[[" ~ link_target ~ ("|" ~ link_display)? ~ "]]" }

embed_inner = {(!("]]" | NEWLINE) ~ ANY)*} 
embed          =  { "![[" ~  embed_inner ~ "]]" }

escaped_char = { "\\" ~ ANY }
soft_break   = { "\\" ~ NEWLINE }
hard_break = _{NEWLINE | &EOI}

heading_sharps = {"#"{1, 6} }
heading        = { &(PEEK ~ "#"+) ~ heading_title_full ~ hard_break ~ body? ~ DROP }
heading_title_full = { PUSH(heading_sharps) ~ " " ~ heading_title }
heading_title  = { (text | paragraph_item)+ }

ruler = { "---" }

frontmatter_inner = { (!(NEWLINE ~ "---" ~ (NEWLINE | &EOI)) ~ ANY)* }
frontmatter       = { "---" ~ NEWLINE ~ frontmatter_inner ~ NEWLINE ~ "---" }

body_item = _{NEWLINE | comment | ruler | callout | quote | list_item | code_block | math_block}
body       = _{ (!(heading_sharps ~ " ") ~ ((!(escaped_char | soft_break) ~ body_item) | paragraph))* ~ heading*}
main       = _{ SOI ~ frontmatter? ~ PUSH_LITERAL("") ~ body }

comment_inner =  { (!"%%" ~ ANY)* }
// Having to handle comment everywhere would be boilerplateous so it's just a block/line item
comment     = { "%%" ~ comment_inner ~ "%%" }

